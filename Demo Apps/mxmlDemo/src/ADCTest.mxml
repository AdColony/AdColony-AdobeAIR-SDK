<?xml version="1.0"?>
<s:Application
    creationComplete="setTimeout(configureAdColony,1500)"
    xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"
    xmlns:mx="library://ns.adobe.com/flex/mx"
    backgroundColor="0x131313" applicationDPI="160">

    <fx:Script>
        <![CDATA[
            import flash.events.Event;
            import mx.controls.Alert;
            import flash.system.Capabilities;
            import com.adcolony.airadc.*;

            private const IOS_APP_ID: String = "appbdee68ae27024084bb334a";
            private const IOS_VIDEO_ZONE: String = "vzf8fb4670a60e4a139d01b5";
            private const IOS_V4VC_ZONE: String = "vzf8e4e97704c4445c87504e";

            private const ANDROID_APP_ID: String = "app185a7e71e1714831a49ec7";
            private const ANDROID_VIDEO_ZONE: String = "vz06e8c32a037749699e7050";
            private const ANDROID_V4VC_ZONE: String = "vz1fd5a8b2bf6841a0a4b826";

            public static var AdColony: AirAdColony;
            private var interstitialVideoAd:AdColonyInterstitial;
            private var interstitialV4vcAd:AdColonyInterstitial;
            private var currAppId: String;
            private var currVideoZone: String;
            private var currV4vcZone: String;
            private var currRewardName: String;
            private var currRewardAmount: Number;

            public function configureAdColony():void {
                AdColony = new AirAdColony();
                if (AdColony.isSupported()) {
                    AdColony.addEventListener(AdColonyInterstitialEvent.EVENT_TYPE, handleInterstitialEvent);
                    AdColony.addEventListener(AdColonyRewardEvent.EVENT_TYPE, handleRewardEvent);

                    if (AdColony.isIos) {
                        currAppId = IOS_APP_ID;
                        currVideoZone = IOS_VIDEO_ZONE;
                        currV4vcZone = IOS_V4VC_ZONE;
                    } else {
                        currAppId = ANDROID_APP_ID;
                        currVideoZone = ANDROID_VIDEO_ZONE;
                        currV4vcZone = ANDROID_V4VC_ZONE;
                    }
                    AdColony.configure(null, currAppId, currVideoZone, currV4vcZone);
                } else {
                    trace("AdColony not supported.");
                }
            }

            //AdColony Event Handlers
            public function handleInterstitialEvent(adEvent:AdColonyInterstitialEvent):void {
                var interstitialAd:AdColonyInterstitial = adEvent.getInterstitialAd();
                var zone:AdColonyZone = adEvent.getZone();

                var zoneStr:String = "";
                if (interstitialAd != null) {
                    zoneStr = interstitialAd.getZoneId();
                    if (zoneStr == currVideoZone) {
                        interstitialVideoAd = interstitialAd;
                    } else if (zoneStr == currV4vcZone) {
                        interstitialV4vcAd = interstitialAd;
                    }
                } else if (zone != null) {
                    zoneStr = zone.getZoneId();
                }

                if (adEvent.getInterstitialEventType() == AirAdColonyDefines.INTERSTITIAL_EVENT_ON_CLICK) {
                    updateButtonText("Clicked", zoneStr);
                } else if (adEvent.getInterstitialEventType() == AirAdColonyDefines.INTERSTITIAL_EVENT_ON_CLOSE) {
                    updateButtonText("Closed", zoneStr);
                    if (zoneStr == currVideoZone) {
                        interstitialVideoAd = null;
                    } else if (zoneStr == currV4vcZone) {
                        interstitialV4vcAd = null;
                    }
                } else if (adEvent.getInterstitialEventType() == AirAdColonyDefines.INTERSTITIAL_EVENT_ON_EXPIRING) {
                    updateButtonText("Expiring", zoneStr);
                } else if (adEvent.getInterstitialEventType() == AirAdColonyDefines.INTERSTITIAL_EVENT_ON_IAP) {
                    updateButtonText("on IAP", zoneStr);
                } else if (adEvent.getInterstitialEventType() == AirAdColonyDefines.INTERSTITIAL_EVENT_ON_LEFT_APP) {
                    updateButtonText("Left App", zoneStr);
                } else if (adEvent.getInterstitialEventType() == AirAdColonyDefines.INTERSTITIAL_EVENT_ON_OPEN) {
                    updateButtonText("Opened", zoneStr);
                } else if (adEvent.getInterstitialEventType() == AirAdColonyDefines.INTERSTITIAL_EVENT_ON_REQUEST_FILLED) {
                    updateButtonText("Request Filled", zoneStr);
                    if (interstitialV4vcAd != null) {
                        AdColony.showInterstitial(interstitialV4vcAd);
                    } else if (interstitialVideoAd != null) {
                        AdColony.showInterstitial(interstitialVideoAd);
                    } else {
                        trace("No Ad Fill");
                    }
                } else if (adEvent.getInterstitialEventType() == AirAdColonyDefines.INTERSTITIAL_EVENT_ON_REQUEST_NOT_FILLED) {
                    updateButtonText("Request not Filled", zoneStr);
                }
            }

            public function handleRewardEvent(rewardEvent:AdColonyRewardEvent):void {
                var reward:AdColonyReward = rewardEvent.getReward();
                var newRewardName:String = reward.getRewardName();
                var newRewardAmount:Number = reward.getRewardAmount();
                if (currRewardName == null || newRewardName != currRewardName) {
                    currRewardAmount = 0;
                    currRewardName = newRewardName;
                }
                currRewardAmount += newRewardAmount;
                v4vcCounterLabel.text = "V4VC Info: " + currRewardAmount + " " + currRewardName;
            }

            public function updateButtonText(adText: String, updated_zone: String):void {
                if (updated_zone == currVideoZone) {
                    videoButton.label = "Play Video -- "+ adText;

                } else if (updated_zone == currV4vcZone) {
                    v4vcButton.label = "Play V4VC -- "+ adText;
                }
            }

            private function playVideoAd(event:Event):void {
                var isSuccess:Boolean = AdColony.requestInterstitial(currVideoZone, null);
                trace("request video ad result = " + isSuccess);
                videoButton.label = "Play Video Ad -- Requested";
            }

            private function playV4vcAd(event:Event):void {
                var isSuccess:Boolean = AdColony.requestInterstitial(currV4vcZone, null);
                trace("request video ad result = " + isSuccess);
                v4vcButton.label = "Play Rewarded Ad -- Requested";
            }
        ]]>
    </fx:Script>

    <!-- Application Layout -->
        <s:VGroup id="mainGroup" horizontalAlign="center" width="100%" >
        <mx:Spacer height="15%" />
        <mx:Spacer height="15%" />
        <s:Image id="adcLogo" source="@Embed('res/img/adc.png')" horizontalAlign="center" scaleMode="letterbox" fillMode="scale"/>
        <mx:Spacer height="15%" />
        <s:VGroup >
        <s:Button skinClass="buttonSkin" id="videoButton"  label="Play Video -- NOT READY" click="playVideoAd(event);"/>
        </s:VGroup>
        <s:VGroup >
        <s:Button skinClass="buttonSkin" id="v4vcButton"  label="Play V4VC -- NOT READY" click="playV4vcAd(event);"/>
        </s:VGroup>
        <s:Label id="v4vcCounterLabel" color="0xFFFFFF" text="V4VC Info: Loading..."/>
        </s:VGroup>

</s:Application>
